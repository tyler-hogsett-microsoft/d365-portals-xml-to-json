(
    $parseLinkEntity := function($entity) {
        $exists($entity)
            ? {
                "name": $entity.name,
                "from": $entity.from,
                "to": $entity.to,
                "filter": $exists($entity.filter)
                    ? [ $entity.filter.condition ]
                    : $null,
                "linkEntity": $exists($entity."link-entity")
                    ? $parseLinkEntity($entity."link-entity")
                    : $null
            }
            : $null
    };
    {
        "entities": entities[0].entity {
            name: {
                "displayName": displayname,
                "disablePlugins": disableplugins,
                "primaryNameField": primarynamefield,
                "fields": fields.field {
                    name: {
                        "customField": customfield,
                        "displayName": displayname,
                        "lookupType": lookupType,
                        "type": type,
                        "updateCompare": updateCompare
                    }
                },
                "relationships": $exists(relationships.relationship)
                    ? relationships.relationship {
                        name: {
                            "isReflexive": isreflexive,
                            "manyToMany": manyToMany,
                            "manyToManyTargetEntity": m2mTargetEntity,
                            "relatedEntity": relatedEntityName
                        }
                    }
                    : $null,
                "filter": $exists(filter)
                    ? {
                        "linkEntity": $parseLinkEntity(filter.fetch.entity."link-entity")
                    }
                    : $null
            }
        }
    }
)